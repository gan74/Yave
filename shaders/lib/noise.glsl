#ifndef NOISE_GLSL
#define NOISE_GLSL

#include "utils.glsl"

// https://www.shadertoy.com/view/4djSRW
// https://www.shadertoy.com/view/llGSzw
// https://www.shadertoy.com/view/4tXyWN

uint hash_1_1(uint n)  {
    // integer hash copied from Hugo Elias
    n = (n << 13U) ^ n;
    n = n * (n * n * 15731U + 789221U) + 1376312589U;
    return n;
}

uint hash_1_2(uvec2 x) {
    uvec2 q = 1103515245U * ((x >> 1U) ^ x.yx);
    uint n = 1103515245U * (q.x ^ (q.y >> 3U));
    return n;
}


float hash_1_1(float p) {
    p = fract(p * 0.1031);
    p *= p + 33.33;
    p *= p + p;
    return fract(p);
}

float hash_1_2(vec2 p) {
    vec3 p3  = fract(vec3(p.xyx) * 0.1031);
    p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.x + p3.y) * p3.z);
}

vec2 hash_2_3(vec3 p3) {
    p3 = fract(p3 * vec3(0.1031, 0.1030, 0.0973));
    p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.xx + p3.yz) * p3.zy);
}

#endif

